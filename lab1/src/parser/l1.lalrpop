use std::str::FromStr;
use crate::parser::ast::*;

grammar;

pub Program: Vec<Box<AstNode>> = {
    "int" "main" "(" ")" "{" "}" => Vec::new(),
    "int" "main" "(" ")" "{" <Stmts> "}" => <>,
};

Stmts: Vec<Box<AstNode>> = {
    <ss:Stmts?> <s:Stmt> => {
        match ss {
            None => vec![s],
            Some(ss) => {
                let mut ss = ss;
                ss.push(s);
                ss
            }
        }
    }
}

Term: Box<AstNode> = {
    "(" <Expr> ")"=> <>,
    <Num> => Box::new(Expr::Num(<>).into()),
    <Ident> => Box::new(Expr::Ident(<>).into())
};

Tier<Op, NextTier>: Box<AstNode> = {
    Tier<Op, NextTier> Op NextTier => Box::new(Expr::Binop(<>).into()),
    NextTier
};

// This is "pub" only because of test reason for that expression is tricky to parse right
pub(crate) Expr: Box<AstNode> = {
    Tier<Binop, Factor> => <>,
    "-" <Expr> => Box::new(Expr::Minus(<>).into()),
};

Factor = Tier<FactorOp, Term>;

Lvalue: Box<AstNode> = {
    <Ident> => Box::new(Lvalue::Ident(<>.to_string()).into()),
    "(" <Lvalue> ")" => Box::new(Lvalue::Lvalue(<>).into()),
};

Decl: Box<AstNode> = {
    "int" <Ident> => Box::new(Decl::Ident(<>.to_string()).into()),
    "int" <i:Ident> "=" <e:Expr> => Box::new(Decl::IdentInit(i.to_string(), e).into()),
};

Stmt: Box<AstNode> = {
    <Decl> ";" => Box::new(Stmt::Decl(<>).into()),
    <l:Lvalue> <op:Asnop> <e:Expr> ";" => Box::new(Stmt::Simp(l, op, e).into()),
    "return" <Expr> ";" => Box::new(Stmt::Return(<>).into()),
};

// lexical tokens
Ident: String = <ident> => <>.to_string();
Num: u32 = {
    <DecNum> => <>,
    <HexNum> => <>,
};
DecNum: u32 = {
    "0" => 0,
    <decnum> => u32::from_str(<>).unwrap(),
};
HexNum: u32 = <hexnum> => u32::from_str_radix(&<>[2..], 16).unwrap();

Binop: Box<AstNode> = {
    "+" => Box::new(Binop::Add.into()),
    "-" => Box::new(Binop::Minus.into()),
};

FactorOp: Box<AstNode>= {
    "*" => Box::new(Binop::Mul.into()),
    "/" => Box::new(Binop::Div.into()),
    "%" => Box::new(Binop::Mod.into()),
};

Asnop: Box<AstNode> = {
    "=" => Box::new(Asnop::Equal.into()),
    "+=" => Box::new(Asnop::AddEq.into()),
    "-=" => Box::new(Asnop::MinEq.into()),
    "*=" => Box::new(Asnop::MulEq.into()),
    "/=" => Box::new(Asnop::DivEq.into()),
    "%=" => Box::new(Asnop::ModEq.into()),
};
// TODO: add reserved words check (reserved cannot be used as identifier)
// Reserved: Stmt = <r"(struct|typedef|if|else|while|for|continue|break|return|assert|true|false|NULL|alloc|alloc_array|int|bool|void|char|string)"> => Stmt:Error(<>.to_string());

match {
    "main",
    "int",
    "(",
    ")",
    "{",
    "}",
    ";",
    "return",
    "0",
    "+",
    "*",
    "-",
    "/",
    "%",
    "+=",
    "-=",
    "*=",
    "/=",
    "%=",
    "=",
    r"[A-Za-z_][A-Za-z0-9]*" => ident,
    r"[1-9][0-9]*" => decnum,
    r"0[xX][0-9a-fA-F]+" => hexnum,
}
